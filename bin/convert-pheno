#!/usr/bin/env perl
#
#   A script to interconvert common data models for phenotypic data
#
#   This file is part of Convert::Pheno
#
#   Last Modified: Sep/02/2022
#
#   Version 0.0.0b
#
#   Copyright (C) 2022 Manuel Rueda (manuel.rueda@cnag.crg.eu)
#
#   This program is free software; you can redistribute it and/or modify
#   it under the terms of the GNU General Public License as published by
#   the Free Software Foundation; either version 3 of the License, or
#   (at your option) any later version.
#
#   This program is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
#   GNU General Public License for more details.
#
#   You should have received a copy of the GNU General Public License
#   along with this program; if not, see <https://www.gnu.org/licenses/>.
#
#   If this program helps you in your research, please cite.

package main;

use strict;
use warnings;
use feature qw(say);
use autodie;
use Getopt::Long;
use Pod::Usage;
use Data::Dumper;
use File::Spec::Functions qw(catdir catfile);
use FindBin               qw($Bin);
use lib "$Bin/../lib";
use Term::ANSIColor qw(:constants);
use List::Util      qw(any);
use Convert::Pheno;
$Data::Dumper::Sortkeys = 1;

# Defining a few variables
my $out_dir = '.';
my $format  = 'json';

# Reading arguments
GetOptions(
    'ipxf=s'                  => \my $in_pxf,                               # string
    'ibff=s'                  => \my $in_bff,                               # string
    'iredcap=s'               => \my $in_redcap,                            # string
    'iomop=s{1,}'             => \my @omop_files,                           # array
    'obff=s'                  => \my $out_bff,                              # string
    'opxf=s'                  => \my $out_pxf,                              # string
    'out-dir=s'               => \$out_dir,                                 # string
    'help|?'                  => \my $help,                                 # flag
    'man'                     => \my $man,                                  # flag
    'debug=i'                 => \my $debug,                                # integer
    'verbose'                 => \my $verbose,                              # flag
    'format=s'                => \$format,                                  # string
    'no-color|nc'             => \my $no_color,                             # flag
    'separator|sep=s'         => \my $sep,                                  # str
    'sql2csv'                 => \my $sql2csv,                              # flag
    'ohdsi-db'                => \my $ohdsi_db,                             # flag
    'redcap-dictionary|rcd=s' => \my $redcap_dictionary,                    # string
    'print-hidden-labels|phl' => \my $print_hidden_labels,                  # flag
    'version|v'               => sub { say "$0 Version $VERSION"; exit; }
) or pod2usage(2);
pod2usage(1)                              if $help;
pod2usage( -verbose => 2, -exitval => 0 ) if $man;
pod2usage(
    -message => "Please specify a valid input file(s) -i <json|csv>\n",
    -exitval => 1
) unless ( $in_pxf || $in_bff || $in_redcap || @omop_files );
pod2usage(
    -message => "Please specify a valid directory for --out-dir\n",
    -exitval => 1
) if ( !-d $out_dir );
pod2usage(
    -message => "Please specify a valid REDCap dictionary --rcd\n",
    -exitval => 1
) if ( $in_redcap && !$redcap_dictionary );
pod2usage(
    -message => "Please specify a valid OMOP-CDM file(s) (e.g, *csv or .sql)\n",
    -exitval => 1
) if ( @omop_files && $omop_files[0] !~ m/\.csv|\.sql/i );
pod2usage(
    -message =>
      "OHDSI database (<-ohdsi-db>) only works with <-iomop> argument\n",
    -exitval => 1
) if ( $ohdsi_db && !@omop_files );

# Turning color off if argument <--no-color>
$ENV{'ANSI_COLORS_DISABLED'} = 1 if $no_color;

# Defining $out_file
my $out_file =
    $out_pxf ? catfile( $out_dir, $out_pxf )
  : $out_bff ? catfile( $out_dir, $out_bff )
  :            catfile( $out_dir, 'individuals.json' );

# Defining method
my $in_type =
    $in_pxf     ? 'pxf'
  : $in_bff     ? 'bff'
  : $in_redcap  ? 'redcap'
  : @omop_files ? 'omop'
  :               'bff';
my $out_type = $out_pxf ? 'pxf' : $out_bff ? 'bff' : 'bff';
my $method   = $in_type . '2' . $out_type;

# Defining data as a hashref
my $data = {
    out_dir     => $out_dir,
    in_textfile => 1,
    in_file     => $in_pxf ? $in_pxf
    : $in_bff    ? $in_bff
    : $in_redcap ? $in_redcap
    : undef,
    in_files            => \@omop_files // undef,    # only for Omop
    method              => $method,
    sep                 => $sep,
    sql2csv             => $sql2csv,
    redcap_dictionary   => $redcap_dictionary,
    ohdsi_db            => $ohdsi_db,
    print_hidden_labels => $print_hidden_labels,
    debug               => $debug,
    verbose             => $verbose
};
print Dumper $data if $debug;

# Start printing to STDOUT
say BOLD CYAN program_header($VERSION) if $verbose;

#############################
# START DATA TRANSFORMATION #
#############################

convert( $out_file, $data );

###########################
# END DATA TRANSFORMATION #
###########################

sub convert {

    my ( $o_file, $l_data ) = @_;

    if ($verbose) {
        print BOLD BLUE program_body($l_data);
        say BOLD GREEN "Writing <$o_file> file\n";
    }

    # Creating object
    my $convert = Convert::Pheno->new($l_data);

    # Running $method
    my $method = $l_data->{method};

    # Writing file
    dump_file(
        { filename => $o_file, data => $convert->$method, format => $format } );
}

sub program_header {

    my $version = shift;
    my $str     = <<EOF;
****************************************
*  Phenotypic Data Model Convert Tool  *
*          - CONVERT-PHENO -           *
*          Version: $version             *
*      (C) 2022 Manuel Rueda, PhD      *
*    GNU General Public License v3     *
****************************************
EOF
    return $str;
}

sub program_body {

    my $l_data = shift;
    my $msg    = <<EOF;
==== METHOD: <$l_data->{method}> ====
Processing: <$l_data->{in_file}>
EOF
    return $msg;
}

sub dump_file {

    my $arg = shift;
    if ( $arg->{format} eq 'json' ) {
        write_json( { filename => $arg->{filename}, data => $arg->{data} } );
    }
    else {
        write_yaml( { filename => $arg->{filename}, data => $arg->{data} } );
    }
    return 1;
}

=head1 NAME

B<UNDER DEVELOPMENT>

convert-pheno - A script to interconvert common data models for phenotypic data

=head1 SYNOPSIS

convert-pheno [-i input-type] <infile> [-o output-type] <outfile> [-options]

     Arguments:                       
       -input-type:  
             -ibff                    Beacon v2 JSON file
             -iomop                   OMOP-CDM CSV files or PostgreSQL dump
             -ipxf                    Phenopacket JSON file
             -iredcap                 REDCap CSV file

            (Wish-list)
             #-icdisc                 CDISC CSV file
             #-ifhir                  FHIR CSV file

       -output-type;
             -obff                    Beacon v2 JSON file
             -opxf                    Phenopacket JSON file

     Options:
       -out-dir                       Output (existing) directory
       -h|help                        Brief help message
       -man                           Full documentation
       -debug                         Print debugging (from 1 to 5, being 5 max)
       -verbose                       Verbosity on
       -nc|-no-color                  Don't print colors to STDOUT
       -format                        Output format for the text file [>json|yaml]
       -rcd|redcap-dictionary         Dictionary file (CSV) exported from REDCap
       -phl|print-hidden-labels       Print original values (before DB mapping) of text fields <_labels>
       -ohdsi-db                      Use Athena-OHDSI database (~1.1GB) with -iomop
       -sep|separator                 Delimiter character for CSV files
     
=head1 DESCRIPTION

C<convert-pheno> is a command-line front-end to the CPAN's module L<Convert::Pheno>.

The module will be uploaded to CPAN once the paper is submitted.

=head1 SUMMARY

A script that uses L<Convert::Pheno> to interconvert common data models for phenotypic data

=head1 INSTALLATION

=head2 Containerized

Please download the C<Dockerfile> from the repo:

  wget https://raw.githubusercontent.com/mrueda/Convert-Pheno/main/Dockerfile

And then run:

  docker build -t cnag/convert-pheno:latest .

To run the container (detached) execute:

  docker run -tid --name convert-pheno cnag/convert-pheno:latest

To enter:

  docker exec -ti convert-pheno bash

=head2 Non containerized

The script runs on command-line Linux (tested on Debian-based distribution). Perl 5 is installed by default on Linux, 
but we will install a few CPAN modules with C<cpanminus>.

First we install cpanminus (with sudo privileges):

  sudo apt-get install cpanminus

Then the modules:

  cpanm --sudo --installdeps .

If you prefer to have the dependencies in a "virtual environment" (i.e., install the CPAN modules in the directory of the application) we recommend using the module C<Carton>.

  cpanm --sudo Carton

Then, we can install our dependencies:

  carton install

=head1 HOW TO RUN CONVERT-PHENO

For executing convert-pheno you will need:

=over

=item Input file(s):
      
A text file in one of the accepted formats.

=back

B<Examples:>

 $ bin/convert-pheno -ipxf phenopackets.json -obff individuals.json

 $ $path/convert-pheno -ibff individuals.json -opxf phenopackets.json --out-dir my_out_dir

 $ $path/convert-pheno -iredcap redcap.csv -opxf phenopackets.json --redcap-dictionary redcap_dict.csv

 $ $path/convert-pheno -iomop dump.sql -obff individuals.json 

 $ $path/convert-pheno -iomop *csv -obff individuals.json -sep ','

 $ carton exec -- $path/convert-pheno -ibff individuals.json -opxf phenopackets.json # If using Carton

=head2 COMMON ERRORS AND SOLUTIONS

 * Error message: Foo
   Solution: Bar

 * Error message: Foo
   Solution: Bar

=head1 CITATION

The author requests that any published work that utilizes C<Convert-Pheno> includes a cite to the the following reference:

Rueda, M., (2022). Convert-Pheno: A toolbox to interconvert common data models for phenotypic data [Software]. Available from https://github.com/mrueda/Convert-Pheno

=head1 AUTHOR 

Written by Manuel Rueda, PhD. Info about CNAG can be found at L<https://www.cnag.crg.eu>.

=head1 REPORTING BUGS

Report bugs or comments to <manuel.rueda@cnag.crg.eu>.

=head1 COPYRIGHT

This PERL file is copyrighted. See the LICENSE file included in this distribution.

=cut
