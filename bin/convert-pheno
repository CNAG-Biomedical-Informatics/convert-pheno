#!/usr/bin/env perl
#
#   A script that interconverts phenotypic data between different CDM formats
#
#   Last Modified: Jul/04/2022
#
#   Version 2.0.0
#
#   Copyright (C) 2021-2022 Manuel Rueda (manuel.rueda@crg.eu)
#
#   This program is free software; you can redistribute it and/or modify
#   it under the terms of the GNU General Public License as published by
#   the Free Software Foundation; either version 3 of the License, or
#   (at your option) any later version.
#
#   This program is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
#   GNU General Public License for more details.
#
#   You should have received a copy of the GNU General Public License
#   along with this program; if not, see <https://www.gnu.org/licenses/>.
#
#   If this program helps you in your research, please cite.

package main;

use strict;
use warnings;
use feature qw(say);
use autodie;
use Getopt::Long;
use Pod::Usage;
use Data::Dumper;
use JSON::XS;
use File::Spec::Functions qw(catdir catfile);
use FindBin               qw($Bin);
use lib "$Bin/../lib";
use Path::Tiny;
use Term::ANSIColor qw(:constants);
use Convert::Pheno;
$Data::Dumper::Sortkeys = 1;

my $debug   = 0;
my $verbose = 0;

# Defining a few variables
my $version = '0.0.0b';
my $out_dir = '.';

# Reading arguments
GetOptions(
    'ipxf=s'      => \my $in_pxf,                               # array
    'ibff|s'      => \my $in_bff,                               # string
    'iredcap=s'   => \my $in_redcap,                            # string
    'obff=s'      => \my $out_bff,                              # string
    'opxf=s'      => \my $out_pxf,                              # string
    'out-dir=s'   => \$out_dir,                                 # string
    'help|?'      => \my $help,                                 # flag
    'man'         => \my $man,                                  # flag
    'debug=i'     => \$debug,                                   # integer
    'verbose'     => \$verbose,                                 # flag
    'no-color|nc' => \my $no_color,                             # flag
    'redcap-dictionary|rcd=s' => \my $redcap_dictionary,        # stringÂº
    'version|v'   => sub { say "$0 Version $version"; exit; }
) or pod2usage(2);
pod2usage(1)                              if $help;
pod2usage( -verbose => 2, -exitval => 0 ) if $man;
pod2usage(
    -message => "Please specify a valid input file(s) -i <*json>\n",
    -exitval => 1
) unless ( $in_pxf || $in_bff || $in_redcap );
pod2usage(
    -message => "Please specify a valid directory for --out-dir\n",
    -exitval => 1
) if ( !-d $out_dir );
pod2usage(
    -message => "Please specify a valid RedCap dictionary --rcd\n",
    -exitval => 1
) if (  $in_redcap && ! $redcap_dictionary );

# Turning color off if argument <--no-color>
$ENV{'ANSI_COLORS_DISABLED'} = 1 if $no_color;

# Defining input as a hash ref
my $input = {
      in_type => $in_pxf ? 'pxf'
    : $in_bff    ? 'bff'
    : $in_redcap ? 'redcap'
    : 'bff',
    out_type => $out_pxf ? 'pxf' : $out_bff ? 'bff' : 'bff',
    in_file => $in_pxf ? $in_pxf
    : $in_bff    ? $in_bff
    : $in_redcap ? $in_redcap
    : 'individuals.json',
    out_file => $out_pxf ? catfile( $out_dir, $out_pxf )
    : $out_bff ? catfile( $out_dir, $out_bff )
    :            catfile( $out_dir, 'individuals.json' ),
    redcap_dictionary => $redcap_dictionary
};
my $method = $input->{in_type} . '2' . $input->{out_type};

#print Dumper $input and die;
# Start printing to STDOUT
say BOLD CYAN program_header($version);

#############################
# START DATA TRANSFORMATION #
#############################

print BOLD BLUE program_body( $input->{in_type}, $input->{out_type},
    $input->{in_file} );
say BOLD GREEN "Writing <$input->{out_file}> file\n";
my $convert = Convert::Pheno->new($input);
write_json( $input->{out_file}, $convert->$method );

###########################
# END DATA TRANSFORMATION #
###########################

sub program_body {

    my ( $in_type, $out_type, $file ) = @_;
    my $msg = <<EOF;
==== INPUT: <$in_type> OUTPUT: <$out_type> ====
==== Processing: <$file> ====
EOF
    return $msg;

}

sub program_header {

    my $version = shift;
    my $str     = <<EOF;
****************************************
*  Phenotypic Data Model Convert Tool  *
*          - CONVERT-PHENO -           *
*          Version: $version             *
*      (C) 2022 Manuel Rueda, PhD      *
*    GNU General Public License v3     *
****************************************
EOF
    return $str;
}

=head1 NAME

B<UNDER DEVELOPMENT>

convert-pheno - A script to interconvert phenotypic data between different CDM formats

=head1 SYNOPSIS

convert-pheno [-i input-type] <infile> [-o output-type] <outfile> [-options]

     Arguments:                       
       -input-type:  
             -ipxf                    Phenopacket JSON file
             -ibff                    Beacon v2 JSON file
             -iredcap                 RedCap CSV file

            (Wish-list)
             #-iomop                  OMOP-CDM CSV file
             #-icdisc                 CDISC CSV file
             #-ifhir                  FHIR CSV file

       -output-type;
             -opxf                    Phenopacket JSON file
             -obff                    Beacon v2 JSON file

             (Wish-list)
             #-oomop                  OMOP-CDM CSV file


     Options:
       -out-dir                       Output (existing) directory
       -h|help                        Brief help message
       -man                           Full documentation
       -debug                         Print debugging (from 1 to 5, being 5 max)
       -verbose                       Verbosity on
       -nc|-no-color                  Don't print colors to STDOUT
       -rcd|redcap-dictionary         Dictionary file (CSV) exported from RedCap
 #     -to-array                      Write all data to a JSON array (instead of objects)
     
=head1 DESCRIPTION

convert-pheno  is a command-line frontend to the module Convert::Pheno.

=head1 CITATION

To be defined.

=head1 SUMMARY

A script that uses Convert::Pheno to interconverts phenotypic data between different CDM formats

=head1 HOW TO RUN PHENO-CONVERT

The script runs on command-line Linux (tested on Debian-based distribution). Perl 5 is installed by default on Linux, 
but we will install a few CPAN modules with C<cpanminus>.

First we install cpanminus (with sudo privileges):

   $ sudo apt-get install cpanminus

Then the modules:

   $ cpanm --sudo --installdeps .

If you prefer to have the dependencies in a "virtual environment" (i.e., install the CPAN modules in the directory of the application) we recommend using the module C<Carton>.

   $ cpanm --sudo Carton

Then, we can install our dependencies:

   $ carton install

For executing convert-pheno you will need:

=over

=item Input file(s):
      
A file on once of the accepted formats.

=back

B<Examples:>

   $ ./convert-pheno -ipxf phenopackets.json -obff individuals.json

   $ $path/convert-pheno -ipxf file.json -obff individuals.json --out-dir my_bff_outdir

   $ $path/convert-pheno -iredcap redcap.csv -opxf phenopackets.json

   $ carton exec -- $path/convert-pheno -ibff individuals.json -opxf phenopackets.json # If using Carton

=head2 COMMON ERRORS AND SOLUTIONS

   * Error message: Foo
     Solution: Bar

   * Error message: Foo
     Solution: Bar


=head1 AUTHOR 

Written by Manuel Rueda, PhD. Info about CNAG can be found at L<https://www.cnag.crg.eu>.

=head1 REPORTING BUGS

Report bugs or comments to <manuel.rueda@cnag.crg.eu>.

=head1 COPYRIGHT

This PERL file is copyrighted. See the LICENSE file included in this distribution.

=cut
